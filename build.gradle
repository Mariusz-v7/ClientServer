group 'pl.mrugames.commons'
version '1.1.2'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.21'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.7'
    compile group: 'ch.qos.logback', name: 'logback-core', version: '1.1.7'
    compile group: 'commons-codec', name: 'commons-codec', version: '1.10'
    compile group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'

    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '2.0.2-beta'
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.6.2'
}

idea {
    module {
        testSourceDirs += file('src/test/integration')
    }
}

sourceSets {
    test {
        java {
            srcDir 'src/test/integration'
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.4'
}

task fatJar(type: Jar) {
    baseName = project.name + '-all'
    from {
        configurations.compile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    with jar
}

task telnetExampleJar(type: Jar) {
    baseName = project.name + '-telnet-example'

    from configurations.compile.collect {
        it.isDirectory() ? it : zipTree(it)
    }

    from sourceSets.test.output

    manifest {
        attributes 'Main-Class': 'pl.mrugames.commons.telnet_example.Main'
    }

    with jar
}

task clientExampleJar(type: Jar) {
    baseName = project.name + '-client-example'

    from configurations.compile.collect {
        it.isDirectory() ? it : zipTree(it)
    }

    from sourceSets.test.output

    manifest {
        attributes 'Main-Class': 'pl.mrugames.commons.client_example.Main'
    }
    with jar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        mavenLocal()
    }
}